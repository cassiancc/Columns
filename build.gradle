plugins {
	id "fabric-loom" version "1.14.10"
	id "maven-publish"
}

base {
	archivesName = project.archives_base_name
}

version = project.mod_version
group = project.maven_group

repositories {
	maven {
		name = "Fabric"
		url = "https://maven.fabricmc.net/"
	}
}

loom {
	accessWidenerPath = file("src/main/resources/columns.accesswidener")

	splitEnvironmentSourceSets()
}

fabricApi {
	configureDataGeneration {
		modId = "columns"
		createSourceSet = true
		client = true
	}
}

assemble.dependsOn(runDatagen)

dependencies {
	// Main
	minecraft("com.mojang:minecraft:${project.minecraft_version}")
	mappings("net.fabricmc:yarn:${project.yarn_mappings}:v2")
	modImplementation("net.fabricmc:fabric-loader:${project.loader_version}")

	// Fabric API (helper)
	modImplementation(fabricApi.module("fabric-registry-sync-v0", project.fabric_version))
	modImplementation(fabricApi.module("fabric-data-generation-api-v1", project.fabric_version))
	modImplementation(fabricApi.module("fabric-item-group-api-v1", project.fabric_version))

	// Fabric API (direct)
	modImplementation(fabricApi.module("fabric-object-builder-api-v1", project.fabric_version))
	modImplementation(fabricApi.module("fabric-resource-loader-v0", project.fabric_version))
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

java {
	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

tasks.withType(JavaCompile) {
	options.release = 21
	options.encoding = "UTF-8"
}

jar {
	from "LICENSE"
	exclude ".cache/**"
}